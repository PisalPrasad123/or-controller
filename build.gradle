buildscript {
    repositories {
        jcenter();
        mavenCentral()
    }
    dependencies {
        classpath "de.richsource.gradle.plugins:gwt-gradle-plugin:${gwtGradlePluginVersion}"
    }
}

apply plugin: "java"
apply plugin: "war"
apply plugin: "gwt-compiler"

sourceCompatibility = 1.8
version = projectVersion

repositories {
    jcenter();
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/google-snapshots/"
    }
}
configurations {
    serverDependencies
    clientDependencies.extendsFrom serverDependencies
    compile.extendsFrom clientDependencies
}

dependencies {
    serverDependencies "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
    serverDependencies "org.slf4j:jul-to-slf4j:${slf4jVersion}"
    serverDependencies "ch.qos.logback:logback-classic:${logbackVersion}"

    serverDependencies "org.apache.camel:camel-core:${camelVersion}"
    serverDependencies "org.apache.camel:camel-jetty:${camelVersion}"
    serverDependencies "org.apache.camel:camel-websocket:${camelVersion}"
    serverDependencies "org.apache.camel:camel-ahc-ws:${camelVersion}"
    serverDependencies "org.apache.camel:camel-jackson:${camelVersion}"
    serverDependencies "org.apache.camel:camel-script:${camelVersion}"
    serverDependencies "org.apache.camel:camel-groovy:${camelVersion}"
    serverDependencies "org.hibernate:hibernate-validator:${hibernateValidatorVersion}"

    serverDependencies fileTree("${zwaveProjectDirectory}/build/libs")

    clientDependencies "com.google.gwt:gwt-user:${gwtVersion}"
    clientDependencies "com.google.gwt:gwt-elemental:${gwtVersion}"
    clientDependencies "com.ahome-it:lienzo-core:${lienzoVersion}"
    clientDependencies "org.fusesource.restygwt:restygwt:${restygwtVersion}"
    clientDependencies "com.google.guava:guava-gwt:${guavagwtVersion}"

    gwt("ru.finam:slf4j-gwt:${slf4jGwtVersion}") {
        exclude group: "com.google.gwt"
    }

    testCompile "org.testng:testng:${testngVersion}"
    testCompile ("org.apache.camel:camel-testng:${camelVersion}") {
        exclude group:"org.apache.camel", module: "camel-spring"
        exclude group:"org.apache.camel", module: "camel-test-spring"
    }
}

task serverRun(type: JavaExec, dependsOn: "classes") {
    main = "org.openremote.beta.server.Server"
    classpath = sourceSets.main.runtimeClasspath
}

gwt {
    gwtVersion = project.properties.gwtVersion

    sourceLevel = JavaVersion.VERSION_1_8
    maxHeapSize = "1024M"

    modules("org.openremote.beta.Client")

    test {
        hasGwtTests = false
    }

    compiler {
        style = "PRETTY"
    }

    superDev {

        // Generate minimal JS launcher in the webapp source folder (you might commit this or not, doesn't matter)
        launcherDir = file("src/main/webapp/gwt")

        // Careful, this binds to all interfaces on your machine!
        bindAddress = "0.0.0.0"
    }
}

// TODO Why is this not configurable?
tasks.withType(de.richsource.gradle.plugins.gwt.AbstractGwtActionTask) {

    doFirst {
        mkdir file("src/main/webapp/gwt")
    }

    //args "-logLevel", "TRACE"
    args "-XjsInteropMode", "JS"
    args "-noincremental" // TODO Yep, incremental compile is apparently broken right now in 2.8.0

    // I've had various problems with this cache and GWT snapshot updates, so it's a good idea to make it easily cleanable
    jvmArgs("-Dgwt.persistentunitcachedir=$project.buildDir/gwt/cache")
}

test {
    dependsOn testClasses
    useTestNG() {
        suites "src/test/AllTests.tng.xml"
    }
    // TODO https://github.com/steffenschaefer/gwt-gradle-plugin/issues/89
    classpath = project.sourceSets.test.runtimeClasspath.filter {
        !it.absolutePath.contains("ru.finam")
    }
}

jar {
    exclude("org/openremote/beta/client")
    exclude("**/*.gwt.xml")
    archiveName = "${rootProject.name}-server.jar"
}

war.enabled = false
task assembleClient(type: Jar) {
    dependsOn "compileGwt"
    from(webAppDir) {
        exclude "gwt"
    }
    from("${project.buildDir}/gwt/out") {
        exclude "WEB-INF"
        into "gwt"
    }
    archiveName = "${rootProject.name}-client.jar"
}

task build(type: Copy, overwrite: true) {
    dependsOn("check", "assemble", "jar", "assembleClient")
    into libsDir
    from configurations.serverDependencies.files
}
