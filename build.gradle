buildscript {
    repositories {
        jcenter();
        mavenCentral()
    }
    dependencies {
        classpath "de.richsource.gradle.plugins:gwt-gradle-plugin:0.6"
    }
}

apply plugin: "java"
apply plugin: "war"
apply plugin: "gwt-compiler"

sourceCompatibility = 1.8
version = '1.0'

repositories {
    jcenter();
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/google-snapshots/"
    }
}
configurations {
    serverDependencies
    clientDependencies.extendsFrom serverDependencies
    compile.extendsFrom clientDependencies
}

dependencies {
    serverDependencies "org.slf4j:jcl-over-slf4j:1.7.10"
    serverDependencies "org.slf4j:slf4j-jdk14:1.7.10"

    serverDependencies "org.apache.camel:camel-core:2.15.2"
    serverDependencies "org.apache.camel:camel-jetty:2.15.2"
    serverDependencies "org.apache.camel:camel-jackson:2.15.2"
    serverDependencies "org.apache.camel:camel-script:2.15.2"
    serverDependencies "org.apache.camel:camel-groovy:2.15.2"

    clientDependencies "com.google.gwt:gwt-user:2.8.0-SNAPSHOT"
    clientDependencies "com.ahome-it:lienzo-core:2.0.124-RC1"
    clientDependencies "org.fusesource.restygwt:restygwt:2.0.2"
}

task serverRun(type: JavaExec, dependsOn: "classes") {
    main = "org.openremote.beta.server.Server"
    classpath = sourceSets.main.runtimeClasspath
}

gwt {
    gwtVersion = "2.8.0-SNAPSHOT"

    sourceLevel = JavaVersion.VERSION_1_8
    maxHeapSize = "1024M"

    modules(
            "org.openremote.beta.Editor"
    )

    compiler {
        style = "PRETTY"
    }

    superDev {

        // Generate minimal JS launcher in the webapp source folder (you might commit this or not, doesn't matter)
        launcherDir = file("src/main/webapp/gwt")

        // Careful, this binds to all interfaces on your machine!
        bindAddress = "0.0.0.0"
    }
}
tasks.withType(de.richsource.gradle.plugins.gwt.AbstractGwtActionTask) {

    doFirst {
        mkdir file("src/main/webapp/gwt")
    }

    //args "-logLevel", "TRACE"
    args "-XjsInteropMode", "JS"
    args "-noincremental" // TODO Yep, incremental compile is apparently broken right now in 2.8.0

    // I've had various problems with this cache and GWT snapshot updates, so it's a good idea to make it easily cleanable
    jvmArgs("-Dgwt.persistentunitcachedir=$project.buildDir/gwt/cache")
}

jar {
    exclude("org/openremote/beta/client")
    exclude("**/*.gwt.xml")
    archiveName = "${rootProject.name}-server.jar"
}

war.enabled = false
task assembleClient(type: Jar) {
    dependsOn "compileGwt"
    from(webAppDir) {
        exclude "gwt"
    }
    from("${project.buildDir}/gwt/out") {
        exclude "WEB-INF"
        into "gwt"
    }
    archiveName = "${rootProject.name}-client.jar"
}

task build(type: Copy, overwrite:true) {
    dependsOn("check", "assemble", "jar", "assembleClient")
    into libsDir
    from configurations.serverDependencies.files
}
